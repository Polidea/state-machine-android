apply plugin: 'com.android.library'
apply plugin: 'groovyx.grooid.groovy-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'android-maven'
apply plugin: 'signing'

def console = System.console()

group = 'com.polidea'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc2"

    signingConfigs {
        adaptersConfig {
            storeFile file("${rootDir}/dev-tools/statemachine.keystore")
            storePassword console ? console.readLine("\nKeystore password: ") : ""
            keyAlias console ? console.readLine("\nAlias: ") : ""
            keyPassword console ? console.readLine("\nKey password: ") : ""
        }
    }

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 1
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig android.signingConfigs.adaptersConfig
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// Set up for Sonatype OSS repository
def isReleaseVersion = !version.endsWith('SNAPSHOT')

def siteUrl = 'https://github.com/Polidea/state-machine-android'      // Homepage URL of the library
def gitUrl = 'https://github.com/Polidea/state-machine-android.git'   // Git repository URL

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask(':uploadArchives') }

    sign configurations.archives
}

def uploadUsername = console ? console.readLine("\nUpload username: ") : ''
def uploadPassword = console ? console.readLine("\nUpload password: ") : ''

def artifactId = 'statemachine'

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment ->
            if (isReleaseVersion) {
                signing.signPom(deployment)
            }
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: uploadUsername, password: uploadPassword)
        }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: uploadUsername, password: uploadPassword)
        }

        pom.artifactId = artifactId

        pom.project {
            name 'state-machine-android'
            packaging 'jar'
            description 'A lightweight state machine implementation for Android.'
            url 'https://github.com/Polidea/state-machine-android'

            scm {
                url 'scm:git@github.com:Polidea/state-machine-android.git'
                connection 'scm:git@github.com:Polidea/state-machine-android.git'
                developerConnection 'scm:git@github.com:Polidea/state-machine-android.git'
            }

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://github.com/Polidea/state-machine-android/blob/master/LICENSE'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'Polidea-Dev'
                    email 'it@polidea.com'
                    name 'Polidea'
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'org.codehaus.groovy:groovy-all:2.4.3'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.1'
    testCompile 'org.objenesis:objenesis:2.1'

}
